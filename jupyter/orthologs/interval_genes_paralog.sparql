PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX faldo: <http://biohackathon.org/resource/faldo#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX ss: <http://semanticscience.org/resource/>

SELECT DISTINCT
   #main data
   str(?gene_id) AS ?gene_id
   str(?location_in_text) AS ?location
   "%(chrom)s" AS ?chrom
   str(?begin_pos) AS ?begin_pos
   str(?end_pos) AS ?end_pos
   str(?ensembl_gene_id) AS ?ensembl_gene_id
   str(?ensembl_location_in_text) AS ?ensembl_location
   str(?ensembl_begin_pos) AS ?ensembl_begin_pos
   str(?ensembl_begin_ref) AS ?ensembl_begin_ref
   str(?ensembl_end_pos) AS ?ensembl_end_pos
   str(?ensembl_end_ref) AS ?ensembl_end_ref
   ?path
   IF(?path="PARALOG",?paralog_gene_id, undef) AS ?paralog_gene_id
   ?ortholog_gene_id AS ?ortholog_gene_id
   ?ortholog_location_in_text AS ?ortholog_location
   str(?ortholog_begin_pos) AS ?ortholog_begin_pos
   str(?ortholog_begin_ref) AS ?ortholog_begin_ref
   str(?ortholog_end_pos) AS ?ortholog_end_pos
   str(?ortholog_end_ref) AS ?ortholog_end_ref
FROM NAMED <http://solgenomics.net/genome/Solanum_tuberosum>
FROM NAMED <http://solgenomics.net/genome/Solanum_lycopersicum>
FROM NAMED <http://plants.ensembl.org/Solanum_lycopersicum>
FROM NAMED <http://plants.ensembl.org/Solanum_tuberosum>
WHERE {
   GRAPH ?graphMain {
      ?gene_uri dcterms:identifier ?gene_id;
                a obo:SO_0001217;
                rdfs:label ?label;
                owl:sameAs ?ensembl_gene;
                faldo:location ?location.
      ?location rdfs:label ?location_in_text.
      ?location faldo:begin ?begin;
                faldo:end ?end.
      ?begin faldo:position ?begin_pos;
                faldo:reference/rdfs:label "%(chrom)s"^^xsd:string. 
      ?end faldo:position ?end_pos.     
   }   
   FILTER( (xsd:integer(?begin_pos) > %(beginPos)i ) && (xsd:integer(?end_pos) > %(beginPos)i ) && (xsd:integer(?begin_pos) < %(endPos)i ) && (xsd:integer(?end_pos) < %(endPos)i ) )
   OPTIONAL {
     GRAPH ?graphOrthologOrigin {
       ?ensembl_gene dc:identifier ?ensembl_gene_id;
       faldo:location ?ensembl_location.
       ?ensembl_location rdfs:label ?ensembl_location_in_text.
       ?ensembl_location faldo:begin ?ensembl_begin;
                faldo:end ?ensembl_end.
       ?ensembl_begin faldo:position ?ensembl_begin_pos;
                faldo:reference ?ensembl_begin_ref. 
       ?ensembl_end faldo:position ?ensembl_end_pos;
                faldo:reference ?ensembl_end_ref.  
     }
     OPTIONAL {
       GRAPH ?graphOrthologOrigin {
         ?ensembl_gene ss:SIO_000630 ?paralog_gene.
         ?paralog_gene dc:identifier ?paralog_gene_id.         
         ?source_gene ss:SIO_000558 ?orthologs
         BIND ( IF ( ?source_gene = ?ensembl_gene, "ORTHOLOG", "PARALOG") AS ?path )
         FILTER(?source_gene IN (?ensembl_gene, ?paralog_gene))  
       }    
       GRAPH ?graphOrthologTarget {
         ?orthologs dc:identifier ?ortholog_gene_id;
                faldo:location ?ortholog_location.
         ?ortholog_location rdfs:label ?ortholog_location_in_text.
         ?ortholog_location faldo:begin ?ortholog_begin;
                faldo:end ?ortholog_end.
         ?ortholog_begin faldo:position ?ortholog_begin_pos;
                faldo:reference ?ortholog_begin_ref. 
         ?ortholog_end faldo:position ?ortholog_end_pos;
                faldo:reference ?ortholog_end_ref.     
       }
     }  
   }
}
ORDER BY ASC(?begin_pos)
