#+ summary: List genes/proteins annotated with Gene Ontology (GO) terms containing text.
#+ tags:
#+      - CandyGene (Example 4)
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX uniprot: <http://purl.uniprot.org/core/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX so: <http://purl.obolibrary.org/obo/so#>
PREFIX go: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX bif: <bif:>

SELECT
   DISTINCT
   (str(?gene_name) AS ?gene_name)
   (concat('[', ?sgn_gene_id, '](https://solgenomics.net/locus/Solyc00g005000.2/view)') AS ?sgn_gene_id)
   (concat('[', ?uniprot_acc, '](', ?prot, ')') AS ?uniprot_acc)
   (concat('[', ?uniprot_id, '](', ?prot, ')') AS ?uniprot_id)
   (str(?uniprot_des) AS ?uniprot_des)
   (str(?go_term) AS ?go_term)
   (concat('[', ?go_id, '](', ?go, ')') AS ?go_id)
   (str(?go_cat) AS ?go_cat)
WHERE {
   GRAPH <http://www.uniprot.org/proteomes/Solanum_lycopersicum> {
      ?prot uniprot:classifiedWith ?go ;
          uniprot:encodedBy/skos:prefLabel ?gene_name
   }
   GRAPH <http://plants.ensembl.org/Solanum_lycopersicum> {
      ?prot dc:identifier ?uniprot_acc ;
          rdfs:label ?uniprot_id ;
          dc:description ?uniprot_des ;
          ^<http://rdf.ebi.ac.uk/terms/ensembl/CHECKSUM> ?ensembl_prot_id .
       ?ensembl_transcript_id so:translates_to ?ensembl_prot_id ;
          so:transcribed_from/dc:identifier ?sgn_gene_id .
   }
   GRAPH <http://purl.obolibrary.org/obo/go.owl> {
      ?go ?p ?o ;
         rdfs:label ?go_term ;
         go:id ?go_id ;
         go:hasOBONamespace ?go_cat .
      ?o bif:contains ?_text .
      FILTER regex(?go, obo:GO_)
   }
}
ORDER BY ?gene_name
